tankC = d0
tankH = d1
tTSrc = d2
pTSrc = d3
# nRDisp = d3
nCDest = d4
nHDest = d5

def AdvFurnace = 545937711
def vF = 1000
def R = 8.314

def vCFactor = 6100 / 6000
def vHFactor = 6100 / 6000

loop:
    yield()
    # (a) batch read temperature and pressure from furnace (tF/pF)
    fix nF = AdvFurnace.all.TotalMoles.avg
    tF = AdvFurnace.all.Temperature.avg
    #pF = AdvFurnace.all.Pressure.avg

    # (b) read tank temperatures
    tC = tankC.Temperature
    tH = tankH.Temperature

    # (c) read targets (tT/pT),
    tT = tTSrc.Setting
    pT = pTSrc.Setting

    # (d) calculate moles to remove and add from C and H (nR/nC/nH)
    nT = pT * vF / (R * tT)
    nRC = nT * (tT - tH) / (tH - tF) + nF
    nRH = nT * (tT - tC) / (tC - tF) + nF
    #nRH = nT * (tC - tT) / (tF - tC) + nF
    nR = max(0, min(nRC, nRH))
    nI = nT - nF + nR
    tI = (tT * nT - tF * (nF - nR)) / nI
    nHI = nI * (tC - tI) / (tC - tH)
    nCI = nI - nHI

    db.Setting = nR
    nCDest.Setting = nCI
    nHDest.Setting = nHI