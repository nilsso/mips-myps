furnace = d0
tankC   = d1
tankH   = d2
nCPump  = d3
nHPump  = d4
nIAnlzr = d5

def vF = 1000  # furnace volume
def R  = 8.314 # universal gas consant
def dt = 0.5   # time step

# Source volume-mole factor (adjust per setup)
# e.g. 6100  = 1 furnace (6000) + 2 pipes (200) L
def vCFactor = (6200) / 6000
def vHFactor = (6200) / 6000

# PD coefficients and minimum error (perhaps adjust)
def kPF = 0.06
def kDF = 0.02
def kPC = 0.06
def kDC = 0.02
def kPH = 0.06
def kDH = 0.02
def ERRORF = 0.01
def ERRORC = 0.01
def ERRORH = 0.01

# Input target temperature/pressure format: PPPTTT
# ```
# input = db.Setting
# tT    = input % 1000 * 10 K
# input = trunc(input / 1000)
# pT    = input * 100 kPa
# ```
# e.g. 500050 -> tT = 50*10 = 500 K, pT = 500*100 = 50,000 kPa
loop:
    input = db.Setting
    if input < 0:
        db.Setting = 0
    elif input > 0:
        # (a) unpack input temperature and pressure
        input = trunc(input)
        tT = (input % 1000) * 10
        pT = trunc(input / 1000) * 100

        # (b) batch read temperature and pressure from furnace (tF/pF)
        nF = furnace.TotalMoles
        tF = furnace.Temperature

        # (c) read tank temperatures
        tC = tankC.Temperature
        tH = tankH.Temperature

        # (d) calculate moles to remove and add from C and H (nR/nC/nH)
        fix rF, rC, rH
        tag calcMolesTargets:
            nT = pT * vF / (R * tT)
            nRC = nT * (tT - tH) / (tH - tF) + nF
            nRH = nT * (tT - tC) / (tC - tF) + nF
            nR = max(0, max(nRC, nRH))
            nI = nT - nF + nR
            tI = (tT * nT - tF * (nF - nR)) / nI
            nHI = nI * (tC - tI) / (tC - tH)
            nCI = nI - nHI # nCI

            nC = vCFactor * tankC.TotalMoles # calc C moles
            nH = vHFactor * tankH.TotalMoles # calc H moles

            rF = nF - nR # calc F target (rF)
            rC = nC - nCI # calc C target (rC
            rH = nH - nHI # calc H target (rH)

        # (e) remove nR, mix nC and nH
        tag E:
            fix eFPrev = 0 # fix previous F error
            fix eCPrev = 0 # fix previous C error
            fix eHPrev = 0 # fix previous H error

            # triple PD loop! controls reaching rF/C/H moles in F/C/H
            # if rF is reached, start inputting the nI mixture in advance to (f)
            tag PDLoop:
                yield()
                nF = furnace.TotalMoles
                nC = vCFactor * tankC.TotalMoles
                nH = vHFactor * tankH.TotalMoles

                eF = nF - rF
                eC = nC - rC
                eH = nH - rH

                uF = max(0, min(100, (kPF * eF) + (kDF * (eF - eFPrev) / dt)))
                uC = max(0, min(100, (kPC * eC) + (kDC * (eC - eCPrev) / dt)))
                uH = max(0, min(100, (kPH * eH) + (kDH * (eH - eHPrev) / dt)))

                eFPrev = eF
                eCPrev = eC
                eHPrev = eH

                Fdone = (eF <= ERRORF)
                Cdone = (eC <= ERRORC)
                Hdone = (eF <= ERRORH)

                furnace.SettingOutput = Fdone ? 0 : uF
                nCPump .SettingOutput = Cdone ? 0 : uC
                nHPump .SettingOutput = Hdone ? 0 : uH

                nCPump .On = Cdone
                nHPump .On = Hdone

                bgt(eF, ERRORF, PDLoop)
                bgt(eC, ERRORC, PDLoop)
                bgt(eH, ERRORH, PDLoop)

        # (g) input the nC and nH gas
        furnace.SettingInput = 100
        while furnace.TotalMoles > 0:
            yield()
        furnace.SettingInput = 0

        # (?) enjoy
        db.Setting = 0